
buildscript {
  repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
  }
  dependencies {
    classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.5.4"
  }
}

plugins {
  id 'com.github.johnrengelman.shadow' version '1.2.4'
}

apply plugin: 'java'
apply plugin: "com.jfrog.artifactory"
apply plugin: 'maven-publish'
apply plugin: "maven"

repositories {
  mavenLocal()
  jcenter()
  mavenCentral()
}

dependencies {
  compile "com.fasterxml.jackson.core:jackson-databind:2.9.5"

  testCompile 'junit:junit:4.12'
}

ext {
  timestamp = new Date().format('yyyyMMddHHmm')
  gitrev = "git log -1 HEAD --pretty=tformat:%h".execute().text.trim()
}

group "com.schibsted.spt.data"
version "0.0.16.$timestamp.$gitrev"

sourceSets {
    main {
        java {
            srcDirs 'src/main/java'
            srcDirs 'build/javacc'
        }
    }
 }

task jjtree (type: Exec) {
  commandLine 'java', '-cp', 'javacc.jar', 'jjtree', '-OUTPUT_DIRECTORY=build/javacc/com/schibsted/spt/data/jslt/parser', 'src/main/javacc/jstl.jj'
}


task javacc (type: Exec) {
  commandLine 'java', '-cp', 'javacc.jar', 'javacc', '-OUTPUT_DIRECTORY=build/javacc/com/schibsted/spt/data/jslt/parser', 'build/javacc/com/schibsted/spt/data/jslt/parser/jstl.jj.jj'
}
compileJava.dependsOn 'javacc'
javacc.dependsOn 'jjtree'

check {
  doLast {
    println 'Test report in build/reports/tests/test/index.html'
  }
}