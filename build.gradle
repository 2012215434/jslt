
// We are deliberately not using the Schibsted JVM, because then we don't
// have to deal with Artifactory and we don't need username/password in
// Travis, so repo forks will work.

buildscript {
  repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
    maven { url "https://plugins.gradle.org/m2/" } // scoverage
    maven {
        url 'https://artifacts.schibsted.io/artifactory/plugins-release-local'
        credentials {
            username = project.hasProperty("artifactory_user") ?
                    "$artifactory_user" : "$System.env.ARTIFACTORY_USER"
            password = project.hasProperty("artifactory_password") ?
                    "$artifactory_password" : "$System.env.ARTIFACTORY_PWD"
        }
    }
  }
  dependencies {
    classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.2'
    classpath 'com.kncept.junit5.reporter:junit-reporter:1.0.1'

    // Schibsted JVM plugin stuff
    classpath 'org.github.ngbinh.scalastyle:gradle-scalastyle-plugin_2.11:0.8.2',
                'gradle.plugin.org.scoverage:gradle-scoverage:2.1.0',
                'com.schibsted.engprod:schibsted-jvm-plugin:0.2.77',
                'com.netflix.nebula:nebula-ospackage-plugin:3.+'
  }
}

plugins {
  id 'com.github.johnrengelman.shadow' version '1.2.4'
}

apply plugin: 'java'
//apply plugin: 'schibsted.jvm-plugin'
apply plugin: 'org.junit.platform.gradle.plugin'
apply plugin: 'com.kncept.junit5.reporter'

repositories {
  mavenLocal()
  jcenter()
  mavenCentral()
}

dependencies {
  compile "com.fasterxml.jackson.core:jackson-databind:2.9.2"

  // the java regex library has an API so incredibly deficient that we
  // can't use it, so we have to depend on this for now (seriously?!?!)
  compile "org.jruby.joni:joni:2.1.11"

  testCompile("org.junit.jupiter:junit-jupiter-api:5.0.2")
  testRuntime("org.junit.jupiter:junit-jupiter-engine:5.0.2")
}

// java jjtree -OUTPUT_DIRECTORY=build/javacc/com/schibsted/spt/data/jstl2 src/main/javacc/jstl.jj
// java javacc -OUTPUT_DIRECTORY=build/javacc/com/schibsted/spt/data/jstl2 build/javacc/com/schibsted/spt/data/jstl2/jstl.jj.jj

group "com.schibsted.spt.data"
version "0.0.10"

//schibstedJvm {
//    enrichVersion = true
//}

sourceSets {
    main {
        java {
            srcDirs 'src/main/java'
            srcDirs 'build/javacc'
        }
    }
 }

task jjtree (type: Exec) {
  commandLine 'java', '-cp', 'javacc.jar', 'jjtree', '-OUTPUT_DIRECTORY=build/javacc/com/schibsted/spt/data/jstl2', 'src/main/javacc/jstl.jj'
}


task javacc (type: Exec) {
  commandLine 'java', '-cp', 'javacc.jar', 'javacc', '-OUTPUT_DIRECTORY=build/javacc/com/schibsted/spt/data/jstl2', 'build/javacc/com/schibsted/spt/data/jstl2/jstl.jj.jj'
}
compileJava.dependsOn 'javacc'
javacc.dependsOn 'jjtree'
